---
page_title: "nsc_nkey Resource - nsc"
subcategory: ""
description: |-
  Generates a NATS NKey keypair. Use with nsc_operator, nsc_account, or nsc_user resources to create JWTs.
---

# nsc_nkey (Resource)

Generates a NATS NKey keypair. Use with nsc_operator, nsc_account, or nsc_user resources to create JWTs.

## Example Usage

```terraform
# Generate an operator key
resource "nsc_nkey" "operator" {
  type = "operator"
}

# Generate an account key
resource "nsc_nkey" "account" {
  type = "account"
}

# Generate a user key
resource "nsc_nkey" "user" {
  type = "user"
}

# Generate a signing key for the operator
resource "nsc_nkey" "operator_signing" {
  type = "operator"
}

# Use the generated keys with JWT resources
resource "nsc_operator" "main" {
  name         = "MyOperator"
  subject      = nsc_nkey.operator.public_key
  issuer_seed  = nsc_nkey.operator.seed
  signing_keys = [nsc_nkey.operator_signing.public_key]
}

resource "nsc_account" "app" {
  name        = "AppAccount"
  subject     = nsc_nkey.account.public_key
  issuer_seed = nsc_nkey.operator.seed
}

resource "nsc_user" "service" {
  name        = "ServiceUser"
  subject     = nsc_nkey.user.public_key
  issuer_seed = nsc_nkey.account.seed
}
```

## Import

Keys can be imported by providing the seed (private key). The key type is automatically detected from the seed prefix:

```shell
# Import an operator key (seed starts with SO)
terraform import nsc_nkey.operator SOABC123...

# Import an account key (seed starts with SA)
terraform import nsc_nkey.account SAXYZ789...

# Import a user key (seed starts with SU)
terraform import nsc_nkey.user SUJKL456...
```

The provider will parse the seed to determine the key type automatically. No need to specify the type during import.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `type` (String) NKey type: operator, account, or user

### Read-Only

- `id` (String) NKey identifier (public key)
- `public_key` (String) NKey public key
- `seed` (String, Sensitive) NKey seed (private key)
